apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def gitUrl = 'https://github.com/rarnu/rarnubase'
def group = "com.rarnu.base"
def libName = "rarnubase"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name libName
                url gitUrl
                licenses {
                    license {
                        name libName
                        url gitUrl
                    }
                }
                developers {
                    developer {
                        id 'rarnu1985'
                        name 'rarnu'
                        email 'ranru1985@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url gitUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName
        desc = ''
        websiteUrl = gitUrl
        vcsUrl = gitUrl
        licenses = ["GPLv3"]
        publish = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.squareup.okhttp3:okhttp:3.8.0"
    compile "com.android.support:support-v13:25.3.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

project.gradle.startParameter.excludedTaskNames.add('lint')
project.gradle.startParameter.excludedTaskNames.add('compileDebugNdk')
project.gradle.startParameter.excludedTaskNames.add('compilereleaseNdk')