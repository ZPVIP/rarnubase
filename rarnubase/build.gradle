apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.squareup.okhttp3:okhttp:3.8.0"
    compile "com.android.support:support-v13:25.3.1"
    compile "org.apache.commons:commons-io:1.3.2"
    compile "org.apache.commons:commons-compress:1.16.1"
    compile "org.apache.commons:commons-lang3:3.7"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

project.gradle.startParameter.excludedTaskNames.add('lint')
project.gradle.startParameter.excludedTaskNames.add('compileDebugNdk')
project.gradle.startParameter.excludedTaskNames.add('compilereleaseNdk')
project.gradle.startParameter.excludedTaskNames.add('prepareDebugUnitTestDependencies')
project.gradle.startParameter.excludedTaskNames.add('generateDebugAndroidTestSources')

group = 'com.github.rarnu'
version = '1.1.4'

signing {
    sign configurations.archives
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.jni.srcDirs
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name project.name
                packaging 'aar'
                description 'RarnuBase'
                url 'https://github.com/rarnu/rarnubase'

                scm {
                    url 'scm:git@github.com:rarnu/rarnubase.git'
                    connection 'scm:git@github.com:rarnu/rarnubase.git'
                    developerConnection 'git@github.com:rarnu/rarnubase.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://github.com/rarnu/rarnubase/blob/master/LICENSE'
                        distribution 'rarnu'
                    }
                }

                developers {
                    developer {
                        id 'rarnu'
                        name 'rarnu'
                        email '123909566@qq.com'
                    }
                }
            }
        }
    }
}
